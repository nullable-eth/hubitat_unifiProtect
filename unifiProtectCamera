/*

Copyright 2020 - tomw

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-------------------------------------------

Change history:

1.4.0 - tomw - Store images from 'take' in File Manager
1.3.9 - tomw - Save snapWidth and snapHeight to device data, for use by imageServerApp
1.3.8 - tomw - Add camera LED indicator control (capability Switch)
1.3.7 - tomw - Support G4 Doorbell Pro
1.3.6 - tomw - Improved event handling
1.3.4 - tomw - Added light support and "isDark" attribute on cameras and lights
1.3.2 - tomw - Added command for setting recording mode.  Supported values are always/motion/never.
1.2.1 - tomw - Added configurable snapshot size.  Default size is 640 x 360.
1.1.0 - tomw - Added Smart Detect support (for supported cameras only, must be configured in UniFi Protect controller)
1.0.0 - tomw - Initial release

*/

metadata
{
    definition(name: "UniFi Protect Camera", namespace: "tomw", author: "tomw", importUrl: "")
    {
        capability "ImageCapture"
        capability "MotionSensor"
        capability "Switch"
        
        command "on", [[name: "turn on status LED", type:"BOOL", description:"turn on status LED"]]
        command "off", [[name: "turn off status LED", type:"BOOL", description:"turn off status LED"]]
        
        command "clearImages"
        attribute "imageTimestamp", "string"
        
        command "setRecordingMode", ["mode"]
        
        command "takePicture", [[name: "Camera lens to use", type:"ENUM", constraints: knownLensUrls().keySet()]]
        
        attribute "isDark", "enum", ["true", "false"]
        attribute "smartDetectType", "string"
    }
}

preferences
{
    section
    {
        input name: "snapWidth", type: "number", title: "snapshot width", defaultValue: 640
        input name: "snapHeight", type: "number", title: "snapshot height", defaultValue: 360
    }
}

def installed()
{
    sendEvent(name: "motion", value: "inactive")
    sendEvent(name: "smartDetectType", value: "n/a")
    
    device.updateSetting("snapWidth", 640)
    device.updateSetting("snapHeight", 360)
    
    updated()
}

def updated()
{
    updateDataValue("snapWidth", snapWidth.toString())
    updateDataValue("snapHeight", snapHeight.toString())
}

def uninstalled()
{
    deleteImageFile()
}

def processEvent(event)
{
    if(!event)
    {
        return
    }
    
    if(null != event.isDark)
    {
        sendEvent(name: "isDark", value: event.isDark)        
    }
    
    if(null != event.smartDetectTypes)
    {
        smartDetectEvent(event.smartDetectTypes)
    }
    
    if(null != event.isMotionDetected)
    {
        sendEvent(name: "motion", value: event.isMotionDetected ? "active" : "inactive")
        
        if(hasSmartDetect() && !event.isMotionDetected)
        {
            // fail safe -- if we don't have any motion, we definitely don't have smart motion
            smartDetectEvent(null)
        }        
    }
    
    if(null != event.ledSettings?.isEnabled)
    {
        sendEvent(name: "switch", value: event.ledSettings.isEnabled ? "on" : "off")
    }
    
    if(null != event.isSmartDetected)
    {
        if(!event.isSmartDetected)
        {
            smartDetectEvent(null)
        }
    }
}

def smartDetectEvent(smartDetectTypes)
{
    if(null == smartDetectTypes)
    {
        // clear event state to default value
        sendEvent(name: "smartDetectType", value: "waiting")
        return
    }
    
    smartDetectTypes.each
    {
        sendEvent(name: "smartDetectType", value: it)
    }
}

def knownLensUrls()
{
    def urls = 
        [
            "main": "snapshot",
            "package": "package-snapshot"
        ]
    
    return urls
}

def checkCamInfo(ffPath)
{
    if(null != state.getAt(ffPath))
    {
        return state.getAt(ffPath)
    }
    
    def cam = getParent()?.getBootstrap()?.cameras.find() { it.id == getCameraId()}
    if(cam)
    {
        def val = cam.featureFlags?.getAt(ffPath)
        state.putAt(ffPath, val)
        return val
    }
}

def hasSmartDetect()
{
    checkCamInfo("hasSmartDetect")
}

def hasPackageCamera()
{
    checkCamInfo("hasPackageCamera")
}

def takePicture(lens = "main")
{
    try
    {
        if(!knownLensUrls().keySet().contains(lens))
        {
            throw new Exception("\"${lens}\" lens type not supported")
        }
        
        if("package" == lens && !hasPackageCamera())
        {
            throw new Exception("\"${lens}\" lens type not supported")
        }
        
        take(lens)
    }
    catch (Exception e)
    {
        log.warn e.message
        take()
    }
}

def take(lens = "main")
{
    try
    {
        def stream = getParent()?.httpExecWithAuthCheck("GET", parent?.genParamsMain(getCameraSnapSuffix(lens)), true)?.data
        
        if(stream)
        {
            def bSize = stream.available()
            byte[] imageArr = new byte[bSize]
            stream.read(imageArr, 0, bSize)
            def image = hubitat.helper.HexUtils.byteArrayToHexString(imageArr)
            
            if(writeImageToFile(image))
            {
                sendEvent(name: "image", value: filePath(), isStateChange: true)
                sendEvent(name: "imageTimestamp", value: now())
            }
        }
    }
    catch (Exception e)
    {
        log.debug "take() failed: ${e.message}"
    }
}

def clearImages()
{
    deleteImageFile()
    sendEvent(name: "image", value: "n/a")
    sendEvent(name: "displayImage", value: "n/a")   
}

def setRecordingMode(mode)
{
    if(!(["always", "motion", "never", "detections"].contains(mode)))
    {
        log.debug "unsupported recording mode (${mode})"
        return
    }
    
    updateCamera([recordingSettings: [mode: mode]])
}

def on()
{
    updateCamera([ledSettings: [isEnabled: true]])
}

def off()
{
    updateCamera([ledSettings: [isEnabled: false]])
}

def updateCamera(Map data)
{
    try
    {
        getParent()?.httpExecWithAuthCheck("PATCH", parent?.genParamsMain(getCameraBaseUrl(), new groovy.json.JsonOutput().toJson(data)), true)
    }
    catch (Exception e)
    {
        log.debug "updateCamera(${data}) failed: ${e.message}"
    }
}

def getCameraId()
{
    return device.getDeviceNetworkId()?.split('-')?.getAt(0)
}

def getCameraBaseUrl()
{
    return "/proxy/protect/api/cameras/" + getCameraId()
}

def getCameraSnapSuffix(lens = "main")
{
    return getCameraBaseUrl() + "/${knownLensUrls()?.getAt(lens)}?force=true&w=${snapWidth ?: 640}&h=${snapHeight ?: 360}"
}

//////////////////////////////////////
// File system operations
//////////////////////////////////////

def fileName()
{
    return device.getDeviceNetworkId()
}

def filePath()
{
    return "http://127.0.0.1:8080/local/${fileName()}"
}

def writeImageToFile(String image)
{
    if(null == image) { return }
    
    // with great thanks to Jean P. May, Jr. (thebearmay) -- https://raw.githubusercontent.com/thebearmay/hubitat/main/fileMgr.groovy

    def imageType = "text/plain"    
    def boundary = now()?.toString()
    
    def header = ("""--${boundary}\r\nContent-Disposition: form-data; name="uploadFile"; filename="${fileName()}"\r\nContent-Type:${imageType}\r\n\r\n""").getBytes("UTF-8")
    def footer = ("""\r\n\r\n--${boundary}\r\nContent-Disposition: form-data; name="folder"\r\n\r\n--${boundary}--""").getBytes("UTF-8")
    
    java.io.ByteArrayOutputStream stream = new java.io.ByteArrayOutputStream()
    stream.write(header)
    stream.write(image.getBytes("UTF-8"))
    stream.write(footer)
    
    def params =
        [
            uri: 'http://127.0.0.1:8080',
            path: '/hub/fileManager/upload',
            query: [ folder: '/' ],
            requestContentType: "application/octet-stream",
            headers: [ 'Content-Type': "multipart/form-data; boundary=${boundary}" ],
            body: stream.toByteArray(),
            ignoreSSLIssues: true
        ]
    
    def success
    try
    {
        httpPost(params)
        { resp ->
            success = resp?.data?.success
        }
        
        return success
    }
    catch(Exception e)
    {
		log.error "Error writing file: ${e.message}"
	}
}

def deleteImageFile(fileName = fileName())
{
    // thanks -- https://raw.githubusercontent.com/thebearmay/hubitat/main/fileMgr.groovy
    
    def body = groovy.json.JsonOutput.toJson( [name:"${fileName}", type:"file"] )
    params = 
        [
            uri: "http://127.0.0.1:8080",
            path: "/hub/fileManager/delete",
            contentType:"text/plain",
            requestContentType:"application/json",
            body: body
        ]
    
    def success
    try
    {
        httpPost(params)
        { }
    }
    catch(Exception e)
    {
		log.error "Error deleting file: ${e.message}"
	}
}
