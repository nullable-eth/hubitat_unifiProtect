/*

Copyright 2020 - tomw

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-------------------------------------------

Change history:

1.3.4 - tomw - Added light support and "isDark" attribute on cameras and lights
1.3.2 - tomw - Added command for setting recording mode.  Supported values are always/motion/never.
1.2.1 - tomw - Added configurable snapshot size.  Default size is 640 x 360.
1.1.0 - tomw - Added Smart Detect support (for supported cameras only, must be configured in UniFi Protect controller)
1.0.0 - tomw - Initial release

*/

metadata
{
    definition(name: "UniFi Protect Camera", namespace: "tomw", author: "tomw", importUrl: "")
    {
        capability "ImageCapture"
        capability "MotionSensor"
        
        command "motionActive"
        command "motionInactive"
        
        command "clearImages"
        command "setRecordingMode", ["mode"]
        
        attribute "displayImage", "string"
        attribute "isDark", "enum", [true, false]
        attribute "smartDetectType", "string"
    }
}

preferences
{
    section
    {
        input name: "snapWidth", type: "number", title: "snapshot width", defaultValue: 640
        input name: "snapHeight", type: "number", title: "snapshot height", defaultValue: 360
    }
}

def installed()
{
    sendEvent(name: "motion", value: "inactive")
    sendEvent(name: "smartDetectType", value: "n/a")
    
    device.updateSetting("snapWidth", 640)
    device.updateSetting("snapHeight", 360)
}

def processEvent(event)
{
    if(!event)
    {
        return
    }
    
    // if we have a motion event, register it.  otherwise, assume no motion.
    (null != event.lastMotion) ? motionActive() : motionInactive()
    
    if(null != event.isDark)
    {
        sendEvent(name: "isDark", value: event.isDark)        
    }
    
    if(!event.smartDetectTypes?.isEmpty() && (null != event.smartDetectTypes))
    {
        smartDetectEvent(event.smartDetectTypes)
    }
}

def motionActive()
{
    sendEvent(name: "motion", value: "active")
}

def motionInactive()
{
    sendEvent(name: "motion", value: "inactive")
}

def smartDetectEvent(smartDetectTypes)
{
    smartDetectTypes.each
    {
        sendEvent(name: "smartDetectType", value: it, isStateChange: true)
    }
    
    pauseExecution(500)
    sendEvent(name: "smartDetectType", value: "waiting", isStateChange: true)
}

def take()
{
    try
    {
        def stream = getParent()?.httpExecWithAuthCheck("GET", parent?.genParamsMain(getCameraSnapSuffix()), true)?.data
        
        if(stream)
        {
            def bSize = stream.available()
            byte[] imageArr = new byte[bSize]
            stream.read(imageArr, 0, bSize)
            def image = hubitat.helper.HexUtils.byteArrayToHexString(imageArr)
            
            sendEvent(name: "image", value: image)
            
            def encodedImage = imageArr.encodeBase64().toString()
            def displayImage = '<img src="data:image/jpeg;base64,' + encodedImage + '">'
            
            sendEvent(name: "displayImage", value: displayImage)
        }
    }
    catch (Exception e)
    {
        log.debug "take() failed: ${e.message}"
    }
}

def clearImages()
{
    sendEvent(name: "image", value: "n/a")
    sendEvent(name: "displayImage", value: "n/a")   
}

def setRecordingMode(mode)
{
    if(!(["always", "motion", "never"].contains(mode)))
    {
        log.debug "unsupported recording mode (${mode})"
        return
    }
    
    try
    {
        getParent()?.httpExecWithAuthCheck("PATCH", parent?.genParamsMain(getCameraBaseUrl(), new groovy.json.JsonOutput().toJson([recordingSettings: [mode: mode]])), true)
    }
    catch (Exception e)
    {
        log.debug "setRecordingMode() failed: ${e.message}"
    }
}

def getCameraBaseUrl()
{
    def id = device.getDeviceNetworkId()?.split('-')?.getAt(0)
    def baseUrlSuffix = "/proxy/protect/api/cameras/" + id
    
    return baseUrlSuffix
}

def getCameraSnapSuffix()
{
    return getCameraBaseUrl() + "/snapshot?force=true&w=${snapWidth ?: 640}&h=${snapHeight ?: 360}"
}
